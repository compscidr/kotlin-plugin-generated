/*
 * Copyright 2018 Fabian Mastenbroek.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
    id "org.jlleitschuh.gradle.ktlint" version "6.2.1"

    id "java-gradle-plugin"
    id "maven-publish"
    id "com.gradle.plugin-publish" version "0.10.0"
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-gradle-plugin-api:${kotlin_version}"

    compileOnly project(':plugin-compiler')
}

/* Compilation */
jar {
    from(project(":plugin-compiler").sourceSets.main.output.classesDirs)
    from(project(":plugin-compiler").sourceSets.main.output.resourcesDir)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

gradlePlugin {
    plugins {
        generated {
            id = 'nl.fabianm.kotlin.plugin.generated'
            implementationClass = 'nl.fabianm.kotlin.plugin.generated.gradle.GeneratedGradleSubplugin'
        }
    }
}

/* Publishing */
pluginBundle {
    website = 'https://github.com/fabianishere/kotlin-plugin-generated'
    vcsUrl = 'https://github.com/fabianishere/kotlin-plugin-generated.git'
    description = 'A Kotlin compiler plugin that annotates Kotlin-generated methods to signify to code analyzers' +
        ' that these methods have been generated by the compiler.'
    tags = ['kotlin', 'compiler', 'generated', 'plugin', 'coverage', 'test']

    plugins {
        generated {
            id = 'nl.fabianm.kotlin.plugin.generated'
            displayName = 'Kotlin Generated compiler plugin'

        }
    }

    mavenCoordinates {
        groupId = "nl.fabianm.kotlin.plugin.generated"
    }
}

bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.key') ? project.property('bintray.key') : System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'kotlin-plugin-generated'
        name = project.name
        userOrg = 'fabianishere'
        licenses = ['Apache-2.0']
        desc = 'A Kotlin compiler plugin that annotates Kotlin-generated methods to signify to code analyzers' +
            ' that these methods have been generated by the compiler.'
        websiteUrl = 'https://github.com/fabianishere/kotlin-plugin-generated'
        issueTrackerUrl = 'https://github.com/fabianishere/kotlin-plugin-generated/issues'
        vcsUrl = 'https://github.com/fabianishere/kotlin-plugin-generated.git'
        licenses = ['Apache-2.0']
        githubRepo = 'fabianishere/kotlin-plugin-generated'
        githubReleaseNotesFile = 'README.md'
    }
}

bintrayUpload.doFirst {
    publications = ['pluginMaven', 'generatedPluginMarkerMaven']
}
