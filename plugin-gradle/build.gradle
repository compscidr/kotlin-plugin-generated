/*
 * Copyright 2018 Fabian Mastenbroek.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "org.jmailen.kotlinter" version "1.12.0"
    id "com.gradle.plugin-publish" version "0.9.10"
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-gradle-plugin-api:1.2.41"

    compileOnly project(':plugin-compiler')
}

/* Compilation */
jar {
    from(project(":plugin-compiler").sourceSets.main.output.classesDirs)
    from(project(":plugin-compiler").sourceSets.main.output.resourcesDir)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

gradlePlugin {
    plugins {
        generated {
            id = 'nl.fabianm.kotlin.plugin.generated'
            implementationClass = 'nl.fabianm.kotlin.plugin.generated.gradle.GeneratedGradleSubplugin'
        }
    }
}

/* Publishing */
pluginBundle {
    website = 'https://github.com/fabianishere/kotlin-plugin-generated'
    vcsUrl = 'https://github.com/fabianishere/kotlin-plugin-generated.git'
    description = 'A Kotlin compiler plugin that annotates Kotlin-generated methods to signify to code analyzers' +
        ' that these methods have been generated by the compiler.'
    tags = ['kotlin', 'compiler', 'generated', 'plugin', 'coverage', 'test']

    plugins {
        generated {
            id = 'nl.fabianm.kotlin.plugin.generated'
            displayName = 'Kotlin Generated compiler plugin'

        }
    }

    mavenCoordinates {
        groupId = "nl.fabianm.kotlin.plugin.generated"
    }
}
